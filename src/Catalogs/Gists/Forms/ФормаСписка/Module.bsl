#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не Метаданные.Справочники.Gists.Реквизиты.Автор.Тип.СодержитТип(Тип("СправочникСсылка.Пользователи")) Тогда
		ВызватьИсключение "ru = 'Не определен тип автора!'"
	КонецЕсли;
	
	ТекущийАвтор = Пользователи.ТекущийПользователь();
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Автор", ТекущийАвтор);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Теги, "Автор", ТекущийАвтор);
	
	Элементы.ФормаПолучитьССайта.Видимость = Не ПустаяСтрока(ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(
		Пользователи.ТекущийПользователь(), "ТокенGitHub"));
		
	Элементы.Список.СоздатьЭлементыФормыПользовательскихНастроек(Элементы.СписокКомпоновщикНастроекПользовательскиеНастройки);
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементов

&НаКлиенте
Процедура СписокТеговПриИзменении(Элемент = Неопределено)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Теги.Тег", СписокТегов,
		ВидСравненияКомпоновкиДанных.ВСписке,, СписокТегов.Количество());
		
	Теги.КомпоновщикНастроек.Настройки.УсловноеОформление.Элементы[0].Отбор.Элементы[0].ПравоеЗначение = СписокТегов
	
КонецПроцедуры

&НаКлиенте
Процедура ТегиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Строка = СписокТегов.НайтиПоЗначению(ВыбраннаяСтрока);
	Если Строка = Неопределено Тогда
		СписокТегов.Добавить(ВыбраннаяСтрока);
	Иначе
		СписокТегов.Удалить(Строка);
	КонецЕсли;
	
	СписокТеговПриИзменении();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы
// Код процедур и функций
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура АвторизацияGitHub(Команда)
	ОткрытьФорму("Справочник.Gists.Форма.АвторизацияGitHub");
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьССайта(Команда)
	
	ПолучитьССайтаНаСервере();
	
	Элементы.Список.Обновить();
	Элементы.Теги.Обновить();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура ПолучитьССайтаНаСервере()
	Справочники.Gists.ПрочитатьGists();
КонецПроцедуры

#КонецОбласти